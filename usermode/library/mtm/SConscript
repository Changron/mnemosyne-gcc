import sys
sys.path.append('%s/library' % (Dir('#').abspath))
import configuration.mtm

Import('mainEnv')  # read only -- clone if need to modify

buildEnv = configuration.mtm.Environment(mainEnv, mainEnv['BUILD_CONFIG_NAME'])
buildEnv.Append(CCFLAGS = '-fno-omit-frame-pointer')
buildEnv.Append(CPPPATH = ['#library/mnemosyne/include'])
buildEnv.Append(CPPPATH = ['#library/atomic_ops'])
buildEnv.Append(CPPPATH = 'src/sysdeps/linux/x86')
buildEnv.Append(CPPPATH = 'src/sysdeps/linux')
buildEnv.Append(CPPPATH = 'src/sysdeps/x86')
buildEnv.Append(CPPPATH = 'src/sysdeps/posix')
buildEnv.Append(CPPPATH = 'src/sysdeps/generic')
buildEnv.Append(CPPPATH = 'src')
buildEnv.Append(CPPPATH = 'src/common')
buildEnv.Append(LINKFLAGS = ['-g', '-fPIC'])
buildEnv.Append(ASFLAGS = '-DHAVE_CONFIG_H -Wall -g -O2 -fPIC')

if buildEnv['BUILD_DEBUG'] == True:
	buildEnv.Append(CCFLAGS = ' -g -D_MTM_BUILD_DEBUG')
else:
	buildEnv.Append(CCFLAGS = ' -O1 -g')



# Programs to generate VTable mappings to the Intel ABI. Ask Haris.
srcgen_sjlj = buildEnv.Program('generate_sjlj', 'src/srcgen/generate_sjlj.c')
srcgen_vtable = buildEnv.Program('generate_vtable', 'src/srcgen/generate_vtable.c')

# For common source files we need to manually specify the object creation rules 
# to avoid getting the following error:
#   scons: warning: Two different environments were specified for target ... 
#   but they appear to have the same actions: ...

COMMON_OBJS = buildEnv.SharedObject('src/debug', '../common/debug.c')

CC_SRC = Split("""
               src/config.c
               src/gc.c
               src/init.c
               src/intelabi.c
               src/local.c
               src/mode/mode.c
               src/mode/pwb/beginend.c
               src/mode/pwb/memcpy.c
               src/mode/pwb/memset.c
               src/mode/pwb/pwb.c
               src/mode/pwb/barrier.c
	       src/mode/pwb/tmlog_base.c
	       src/mode/pwb/tmlog_tornbit.c
               src/mode/wbetl/barrier.c
               src/mode/wbetl/beginend.c
               src/mode/wbetl/memcpy.c
               src/mode/wbetl/memset.c
               src/mode/wbetl/wbetl.c
               src/mode/serial/serial.c
               src/mtm.c
               src/retry.c
               src/useraction.c
               """)

CC_LINUX_SRC = Split("""
                     src/sysdeps/linux/rwlock.c
                     src/sysdeps/linux/futex.c
                     """)

S_SRC = Split("""src/sysdeps/x86/sjlj.S""")

SRC = CC_SRC + S_SRC + CC_LINUX_SRC + COMMON_OBJS

if mainEnv['BUILD_LINKAGE'] == 'dynamic':
	mtmLibrary = buildEnv.SharedLibrary('mtm', SRC)
else:
	mtmLibrary = buildEnv.StaticLibrary('mtm', SRC)

Return('mtmLibrary')
